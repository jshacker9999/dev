<!DOCTYPE html>
<html>

<head>
   <meta charset="UTF-8">
   <meta name="viewport" content="width=device-width, initial-scale=1.0">
   <meta http-equiv="X-UA-Compatible" content="ie=edge">
   <title>Reflective Spheres </title>
   <link rel="stylesheet" href="css/reset.css">
   <link rel="stylesheet" href="css/page.css">
   <link href='http://fonts.googleapis.com/css?family=Open+Sans:400,300,600' rel='stylesheet' type='text/css'>
</head>

<body onload="init()">
   <canvas id="canvas" width="768" height="512"></canvas>
   <a href="#" id="fullscreen">Toggle Full Screen</a>

</body>
<script src="js/dat.gui.min.js"></script>
<script src="js/stats.min.js"></script>
<script id="vertex" type="x-shader">
attribute vec2 aVertexPosition;

void main()
{
gl_Position = vec4(aVertexPosition, 0.0, 1.0);
}
   </script>
<script id="fragment" type="x-shader">
#ifdef GL_ES
precision highp float;
#endif

uniform float time;
uniform vec2 resolution;
uniform vec3 cameraPos;
uniform vec3 cameraLookat;
uniform vec3 lightDir;
uniform vec3 lightColour;
uniform float specular;
uniform float specularHardness;
uniform vec3 diffuse;
uniform float ambientFactor;
uniform bool ao;
uniform bool shadows;
uniform bool rotateWorld;
uniform bool antialias;

#define PI 3.14159265
#define AO_SAMPLES 5
#define RAY_DEPTH 512
#define MAX_DEPTH 256.0
#define DISTANCE_MIN 0.001
#define PI 3.14159265

#define FLOOR_YPOS -0.35

const vec2 delta = vec2(DISTANCE_MIN, 0.);


vec3 RotateY(vec3 p, float a)
{
float c,s;
vec3 q=p;
c = cos(a);
s = sin(a);
p.x = c * q.x + s * q.z;
p.z = -s * q.x + c * q.z;
return p;
}

float Plane(vec3 p, vec3 n)
{
return dot(p, n);
}

/*vec3 ReplicateXZ(vec3 p, vec3 c)
{
return vec3(mod(p.x, c.x) - 0.5 * c.x, p.y, mod(p.z, c.z) - 0.5 * c.z);
}
float Spheres(vec3 p, vec3 c)
{
vec3 q1 = ReplicateXZ(p, vec3(4.0, 0.0, 2.0));
vec3 q2 = ReplicateXZ(p + vec3(2.0, 0.0, 1.5), vec3(4.0, 0.0, 2.0));

return
   min(
      length(q1-vec3(0.0,0.5,0.0))-0.75,
      length(q2-vec3(0.0,0.0,0.0))-0.35
   );
}*/

float Spheres(vec3 p, vec3 c)
{
vec3 q = vec3(mod(p.x, c.x) - 0.5 * c.x, p.y, mod(p.z, c.z) - 0.5 * c.z);

return length(q-vec3(0.0,0.5,0.0))-0.85;
}

// This should return continuous positive values when outside and negative values inside,
// which roughly indicate the distance of the nearest surface.
float Dist(vec3 pos)
{
if (rotateWorld) pos = RotateY(pos, sin(time*0.25)*PI);

return min(
   Plane(pos-vec3(0.,FLOOR_YPOS,0.), vec3(0.,1.,0.)),
   Spheres(pos, vec3(3.,0.,3.))
);
}

// Based on original by IQ - optimized to remove a divide
float CalcAO(vec3 p, vec3 n)
{
float r = 0.0;
float w = 1.0;
for (int i=1; i<=AO_SAMPLES; i++)
{
   float d0 = float(i) * 0.3;
   r += w * (d0 - Dist(p + n * d0));
   w *= 0.5;
}
return 1.0 - r;
}

float SoftShadow(vec3 ro, vec3 rd)
{
float k = 16.0;          // softness
float res = 1.0;
float t = 1.0;          // min-t see http://www.iquilezles.org/www/articles/rmshadows/rmshadows.htm
for (int i=0; i<16; i++)
{
   float h = Dist(ro + rd * t);
   res = min(res, k*h/t);
   t += h;
   if (t > 8.0) break; // max-t
}
return clamp(res, 0.0, 1.0);
}

vec3 GetNormal(vec3 pos, float s)
{
if (pos.y < FLOOR_YPOS + DISTANCE_MIN)
{
   return vec3(0.0,1.0,0.0);
}
else
{
   vec3 n;
   n.x = s - Dist(pos - delta.xyy);
   n.y = s - Dist(pos - delta.yxy);
   n.z = s - Dist(pos - delta.yyx);
   
   return normalize(n);
}
}

// Based on a shading method by Ben Weston. Added AO and SoftShadows to original.
vec4 Shading(vec3 pos, vec3 rd, vec3 norm)
{
vec3 light;
float ref;

// simple pos test on pos.y for floor (see Dist() above) - different colour and no spec for floor
if (pos.y > FLOOR_YPOS+DISTANCE_MIN)
{
   light = lightColour * max(0.0, dot(norm, lightDir));
   vec3 heading = normalize(-rd + lightDir);
   float spec = pow(max(0.0, dot(heading, norm)), specularHardness);
   light = (diffuse * light) + (spec * specular);
   //if (shadows) light *= SoftShadow(pos, lightDir);
   if (ao) light += CalcAO(pos, norm) * ambientFactor;
   // sphere reflection
   ref = 0.5;
}
else
{
   light = vec3(0.1,0.66,0.2) * max(0.0, dot(norm, lightDir));
   if (shadows) light *= SoftShadow(pos, lightDir);
   if (ao) light += CalcAO(pos, norm) * ambientFactor;
   // floor reflection level
   ref = 0.2;
}

return vec4(light, ref);
}

// Original method by David Hoskins
vec3 Sky(in vec3 rd)
{
float sunAmount = max(dot(rd, lightDir), 0.0);
float v = pow(1.0 - max(rd.y,0.0),6.);
vec3 sky = mix(vec3(.1, .2, .3), vec3(.32, .32, .32), v);
sky += lightColour * sunAmount * sunAmount * .25 + lightColour * min(pow(sunAmount, 800.0)*1.5, .3);
return sky;
}

// Camera function by TekF
// Compute ray fro8m camera parameters
vec3 GetRay(vec3 dir, vec2 pos)
{
pos = pos - 0.5;
pos.x *= resolution.x/resolution.y;

dir = normalize(dir);
vec3 right = normalize(cross(vec3(0.,1.,0.),dir));
vec3 up = normalize(cross(dir,right));

return dir + right*pos.x + up*pos.y;
}

// March - note the inout!
vec4 March(inout vec3 ro, inout vec3 rd)
{
float t = 0.0;
float d = 1.0;
for (int i=0; i<RAY_DEPTH; i++)
{
   vec3 p = ro + rd * t;
   d = Dist(p);
   if (d < DISTANCE_MIN)
   {
      vec3 norm = GetNormal(p, d);
      vec4 res = Shading(p, rd, norm);
      res.rgb *= (1.0 - res.a);
      
      // ray reflection - shift out again so not immediately hit same point
      rd = reflect(rd, norm);
      ro = p + (rd * DISTANCE_MIN);
      return res;
   }
   t += d;
   if (t >= MAX_DEPTH) break;
}
return vec4(Sky(rd), 0.0);
}

vec4 MarchEnd(vec3 ro, vec3 rd)
{
float t = 0.0;
float d = 1.0;
for (int i=0; i<RAY_DEPTH; i++)
{
   vec3 p = ro + rd * t;
   d = Dist(p);
   if (d < DISTANCE_MIN)
   {
      vec3 norm = GetNormal(p, d);
      return Shading(p, rd, norm);
   }
   t += d;
   if (t >= MAX_DEPTH) break;
}
return vec4(Sky(rd), 0.0);
}

void main()
{
const int ANTIALIAS_SAMPLES = 4;
//const int DOF_SAMPLES = 16;

//const bool dof = false;

vec3 cpos = cameraPos;
if (rotateWorld) cpos.y = sin(time)*4.0 + 4.0;

vec4 res = vec4(0.0);

if (antialias)
{
   vec2 p;
   float d_ang = 2.*PI / float(ANTIALIAS_SAMPLES);
   float ang = d_ang * 0.33333;
   float r = 0.5;
   for (int i = 0; i < ANTIALIAS_SAMPLES; i++)
   {
      p = vec2((gl_FragCoord.x + cos(ang)*r) / resolution.x, (gl_FragCoord.y + sin(ang)*r) / resolution.y);
      vec3 ro = cpos;
      vec3 rd = normalize(GetRay(cameraLookat-cpos, p));
      vec4 _res = March(ro, rd);
      if (_res.a != 0.0)  // reflection sample needed
      {
         vec4 res1 = March(ro, rd);
         if (res1.a != 0.0)  // another reflection sample needed
         {
            // add in colour from another reflection march step
            _res.rgb += res1.rgb * _res.a * 0.666;  // the amount of ref was returned on the first surface hit test - divide by number of reflection passes
            vec4 res2 = MarchEnd(ro, rd);
            _res.rgb += res2.rgb * _res.a * 0.333;  // the amount of ref was returned on the first surface hit test - divide by number of reflection passes
         }
         else
         {
            // add in colour from a reflection march step
            _res.rgb += res1.rgb * _res.a;     // the amount of ref was returned on the first surface hit test - divide by number of reflection passes
         }
      }
      res.rgb += _res.rgb;
      ang += d_ang;
   }
   res.rgb /= float(ANTIALIAS_SAMPLES);
}
else
{
   vec2 p = gl_FragCoord.xy / resolution.xy;
   vec3 ro = cpos;
   vec3 rd = normalize(GetRay(cameraLookat-cpos, p));
   res = March(ro, rd);
   if (res.a != 0.0)  // reflection sample needed
   {
      vec4 res1 = March(ro, rd);
      if (res1.a != 0.0)  // another reflection sample needed
      {
         // add in colour from another reflection march step
         res.rgb += res1.rgb * res.a * 0.666;  // the amount of ref was returned on the first surface hit test - divide by number of reflection passes
         vec4 res2 = MarchEnd(ro, rd);
         res.rgb += res2.rgb * res.a * 0.333;  // the amount of ref was returned on the first surface hit test - divide by number of reflection passes
      }
      else
      {
         // add in colour from a reflection march step
         res.rgb += res1.rgb * res.a;     // the amount of ref was returned on the first surface hit test - divide by number of reflection passes
      }
   }
}

gl_FragColor = vec4(res.rgb, 1.0);
}
   </script>

<script type="text/javascript">
   var requestAnimationFrame = window.requestAnimationFrame || window.webkitRequestAnimationFrame ||
      window.mozRequestAnimationFrame || window.msRequestAnimationFrame ||
      function (c) { window.setTimeout(c, 15) };
   var config = {
      camera: {
         x: 0.0, y: 5.0, z: -3.0
      },
      lookat: {
         x: 0.0, y: 0.5, z: 0.5
      },
      lightDir: {
         x: 2.0, y: 2.4, z: -1.0
      },
      lightColour: {
         r: 1.6, g: 1.8, b: 2.2
      },
      surface: {
         specular: 1.0,
         specularHardness: 16.0,
         diffuse: 0.25,
         ambientFactor: 0.2
      },
      global: {
         ao: true,
         shadows: true,
         rotateWorld: true,
         antialias: "None"// None|Classic
      }
   };

   var aspect, gl;
   function init() {
      var pause = false;
      document.addEventListener('keydown', function (e) {
         switch (e.keyCode) {
            case 27: // ESC
               pause = !pause;
               break;
         }
      }, false);

      // add GUI controls
      var mobile = (navigator.userAgent.indexOf("Android") !== -1);
      var gui = new dat.GUI();
      var panel = gui.addFolder('Camera Position');
      panel.add(config.camera, "x").min(-160.0).max(160.0).step(0.1);
      panel.add(config.camera, "y").min(-16.0).max(16.0).step(0.1);
      panel.add(config.camera, "z").min(-160.0).max(160.0).step(0.1);
      //panel.open();
      panel = gui.addFolder('Camera LookAt');
      panel.add(config.lookat, "x").min(-160.0).max(160.0).step(0.1);
      panel.add(config.lookat, "y").min(-16.0).max(16.0).step(0.1);
      panel.add(config.lookat, "z").min(-160.0).max(160.0).step(0.1);
      //if (!mobile) panel.open();
      panel = gui.addFolder('Light Direction');
      panel.add(config.lightDir, "x").min(-16.0).max(16.0).step(0.1);
      panel.add(config.lightDir, "y").min(-16.0).max(16.0).step(0.1);
      panel.add(config.lightDir, "z").min(-16.0).max(16.0).step(0.1);
      if (!mobile) panel.open();
      panel = gui.addFolder('Light Colour');
      panel.add(config.lightColour, "r").min(0.0).max(3.0).step(0.1);
      panel.add(config.lightColour, "g").min(0.0).max(3.0).step(0.1);
      panel.add(config.lightColour, "b").min(0.0).max(3.0).step(0.1);
      if (!mobile) panel.open();
      panel = gui.addFolder('Surface');
      panel.add(config.surface, "specular").min(0).max(64);
      panel.add(config.surface, "specularHardness").min(16).max(1024).step(16);
      panel.add(config.surface, "diffuse").min(0).max(1).step(0.05);
      panel.add(config.surface, "ambientFactor").min(0).max(1).step(0.05);
      //panel.open();
      panel = gui.addFolder('Global');
      panel.add(config.global, "antialias", ["None", "Classic"]).name("Anti Alias");
      panel.add(config.global, "shadows").name("Shadows");
      panel.add(config.global, "ao").name("Ambient Occlusion");
      panel.add(config.global, "rotateWorld").name("Rotate World");
      if (!mobile) panel.open();

      var stats = new Stats();
      document.body.appendChild(stats.domElement);

      // create webgl context on the canvas element
      var canvas = document.getElementById("canvas");
      aspect = canvas.width / canvas.height;
      try {
         gl = canvas.getContext("experimental-webgl");
      }
      catch (e) {
         document.write("Whoops! No useful WEB-GL impl available. Shame on you and your browser vendor.<br>" + e.message);
         return;
      }
      gl.viewport(0, 0, canvas.width, canvas.height);
      gl.clearColor(0, 0, 0, 1);
      gl.clear(gl.COLOR_BUFFER_BIT);

      // get the vertex and fragment shader source
      var v = document.getElementById("vertex").firstChild.nodeValue;
      var f = document.getElementById("fragment").firstChild.nodeValue;

      // compile and link the shaders
      var vs = gl.createShader(gl.VERTEX_SHADER);
      gl.shaderSource(vs, v);
      gl.compileShader(vs);

      var fs = gl.createShader(gl.FRAGMENT_SHADER);
      gl.shaderSource(fs, f);
      gl.compileShader(fs);

      var program = gl.createProgram();
      gl.attachShader(program, vs);
      gl.attachShader(program, fs);
      gl.linkProgram(program);

      // debug shader compile status
      var error = false;
      if (!gl.getShaderParameter(vs, gl.COMPILE_STATUS)) {
         error = true;
         console.log(gl.getShaderInfoLog(vs));
      }

      if (!gl.getShaderParameter(fs, gl.COMPILE_STATUS)) {
         error = true;
         console.log(gl.getShaderInfoLog(fs));
      }

      if (!gl.getProgramParameter(program, gl.LINK_STATUS)) {
         error = true;
         console.log(gl.getProgramInfoLog(program));
      }
      if (error) return;

      var firstTime = Date.now();
      (f = function () {
         if (!pause) {
            stats.begin();

            var vertices = new Float32Array(
               [
                  -1, 1 * aspect, 1, 1 * aspect, 1, -1 * aspect,
                  -1, 1 * aspect, 1, -1 * aspect, -1, -1 * aspect
               ]);

            var vbuffer = gl.createBuffer();
            gl.bindBuffer(gl.ARRAY_BUFFER, vbuffer);
            gl.bufferData(gl.ARRAY_BUFFER, vertices, gl.STATIC_DRAW);

            var triCount = 2,
               numItems = vertices.length / triCount;

            gl.useProgram(program);

            var time = (Date.now() - firstTime) / 1000.0;
            program.time = gl.getUniformLocation(program, "time");
            gl.uniform1f(program.time, time);

            program.resolution = gl.getUniformLocation(program, "resolution");
            gl.uniform2f(program.resolution, canvas.width, canvas.height);

            program.cameraPos = gl.getUniformLocation(program, "cameraPos");
            gl.uniform3f(program.cameraPos, config.camera.x, config.camera.y, config.camera.z);

            program.cameraLookat = gl.getUniformLocation(program, "cameraLookat");
            gl.uniform3f(program.cameraLookat, config.lookat.x, config.lookat.y, config.lookat.z);

            program.lightDir = gl.getUniformLocation(program, "lightDir");
 
            var x = config.lightDir.x, y = config.lightDir.y, z = config.lightDir.z;
            var len = x * x + y * y + z * z;
            len = 1.0 / Math.sqrt(len);
            gl.uniform3f(program.lightDir, config.lightDir.x * len, config.lightDir.y * len, config.lightDir.z * len);

            program.lightColour = gl.getUniformLocation(program, "lightColour");
            gl.uniform3f(program.lightColour, config.lightColour.r, config.lightColour.g, config.lightColour.b);

            program.specular = gl.getUniformLocation(program, "specular");
            gl.uniform1f(program.specular, config.surface.specular);

            program.specularHardness = gl.getUniformLocation(program, "specularHardness");
            gl.uniform1f(program.specularHardness, config.surface.specularHardness);

            program.diffuse = gl.getUniformLocation(program, "diffuse");
            gl.uniform3f(program.diffuse, config.surface.diffuse, config.surface.diffuse, config.surface.diffuse);

            program.ambientFactor = gl.getUniformLocation(program, "ambientFactor");
            gl.uniform1f(program.ambientFactor, config.surface.ambientFactor);

            program.rotateWorld = gl.getUniformLocation(program, "rotateWorld");
            gl.uniform1f(program.rotateWorld, config.global.rotateWorld);

            program.antialias = gl.getUniformLocation(program, "antialias");
            gl.uniform1f(program.antialias, (config.global.antialias === "Classic"));

            program.ao = gl.getUniformLocation(program, "ao");
            gl.uniform1f(program.ao, config.global.ao);

            program.shadows = gl.getUniformLocation(program, "shadows");
            gl.uniform1f(program.shadows, config.global.shadows);

            program.aVertexPosition = gl.getAttribLocation(program, "aVertexPosition");
            gl.enableVertexAttribArray(program.aVertexPosition);
            gl.vertexAttribPointer(program.aVertexPosition, triCount, gl.FLOAT, false, 0, 0);

            gl.drawArrays(gl.TRIANGLES, 0, numItems);

            stats.end();
         }
         requestAnimationFrame(f);
      })();
   }
</script>
<script src="js/utils.js"></script>

</html>