<!DOCTYPE html>
<html>

<head>
   <meta charset="UTF-8">
   <meta name="viewport" content="width=device-width, initial-scale=1.0">
   <meta http-equiv="X-UA-Compatible" content="ie=edge">
   <title>Reflective materials over water</title>
   <link rel="stylesheet" href="css/reset.css">
   <link rel="stylesheet" href="css/page.css">
   <link href='http://fonts.googleapis.com/css?family=Open+Sans:400,300,600' rel='stylesheet' type='text/css'>
</head>

<body onload="init()">
   <canvas id="canvas" width="720" height="480"></canvas>
   <a href="#" id="fullscreen">Toggle Full Screen</a>

</body>
<script src="js/dat.gui.min.js"></script>
<script src="js/stats.min.js"></script>
<script id="vertex" type="x-shader">
attribute vec2 aVertexPosition;

void main()
{
gl_Position = vec4(aVertexPosition, 0.0, 1.0);
}
</script>
<script id="fragment" type="x-shader">
#ifdef GL_ES
precision highp float;
#endif

uniform float time;
uniform vec2 resolution;
uniform vec3 cameraPos;
uniform vec3 cameraLookat;
uniform vec3 lightDir;
uniform vec3 lightColour;
uniform float specular;
uniform float specularHardness;
uniform vec3 diffuse;
uniform float ambientFactor;
uniform bool rotateWorld;
uniform float param;
//uniform bool antialias;

#define PI 3.14159265
#define AO_SAMPLES 4

#define RAY_DEPTH 150
#define MAX_DEPTH 100.0
#define DISTANCE_MIN 0.001
#define PI 3.14159265

#define MATERIAL_SKY 0.0
#define MATERIAL_WATER 1.0
#define MATERIAL_METAL 2.0

const vec2 delta = vec2(DISTANCE_MIN, 0.);

float Hash(in float n)
{
return fract(sin(n)*43758.5453123);
}

float Noise(in vec2 x)
{
vec2 p = floor(x);
vec2 f = fract(x);
f = f*f*(3.0-2.0*f);
float n = p.x + p.y*57.0;
float res = mix(mix( Hash(n+  0.0), Hash(n+  1.0),f.x),
              mix( Hash(n+ 57.0), Hash(n+ 58.0),f.x),f.y);
return res;
}

// Octave transform matrix from Alexander Alekseev aka TDM 
mat2 octave_m = mat2(1.6,1.2,-1.2,1.6);

float FractalNoise(in vec2 xy)
{
float m = 1.5;
float w = 0.5;
float f = 0.0;
for (int i = 0; i < 6; i++)
{
f += Noise(xy+time*0.511) * m * 0.175;
f += Noise(xy.yx-time*0.333) * w * 0.25;
w *= 0.5;
m *= 0.25;
xy *= octave_m;
}
return f;
}

vec3 RotateY(vec3 p, float a)
{
float c,s;
vec3 q=p;
c = cos(a);
s = sin(a);
p.x = c * q.x + s * q.z;
p.z = -s * q.x + c * q.z;
return p;
}

const float rr = 1.0/sqrt(3.0);
float CubeFrame(vec3 p, vec3 c, float r)
{
r = r*mix(1.0,rr,.5);
p -= c;
p = abs(p);
float rr = r * .15;   // edge
p -= vec3(r-rr);
// whichever axis is most negative should be clamped to 0
if ( p.x < p.z ) p = p.zyx;
if ( p.y < p.z ) p = p.xzy;
p.z = max(0.0,p.z);
return length(p)-rr;
}

float pModPolar(inout vec2 p, float repetitions) {
float angle = 2.0*PI/repetitions;
float a = atan(p.y, p.x) + angle/2.0;
float r = length(p);
float c = floor(a/angle);
a = mod(a,angle) - angle/2.;
p = vec2(cos(a), sin(a))*r;
if (abs(c) >= (repetitions/2.)) c = abs(c);
return c;
}

const float POLAR = 24.0;
vec2 Objects(vec3 p)
{
vec2 polar = vec2(p.xz);
float cell = 1./POLAR * (pModPolar(polar, POLAR) + POLAR/2.0);
vec3 q1 = vec3(polar.x-6.0, p.y, polar.y);

if (rotateWorld) q1 = RotateY(q1, sin(time*0.5)*PI);

float d = min(
CubeFrame(q1, vec3(0.0, 1.5+sin(time)*0.8 ,0.0), 0.6),
length(q1-vec3(0.0,1.5,0.0))-0.333
);

return vec2(d, MATERIAL_METAL + cell);
}

vec2 Water(vec3 pos)
{
return vec2(dot(pos - vec3(0.,-FractalNoise(pos.xz),0.), vec3(0.,1.,0.)), MATERIAL_WATER);
}

vec2 DistObjects(vec3 pos)
{
return Objects(pos);
}

vec2 min2(vec2 a, vec2 b) { return a.x < b.x ? a : b; }
vec2 DistAll(vec3 pos)
{
return min2(
Objects(pos),
Water(pos)
);
}

float CalcAO(vec3 p, vec3 n)
{
float r = 0.0;
float w = 1.0;
for (int i=1; i<=AO_SAMPLES; i++)
{
float d0 = float(i) * 0.3;
r += w * (d0 - DistObjects(p + n * d0).x);
w *= 0.5;
}
return 1.0 - r;
}

float SoftShadow(vec3 ro, vec3 rd)
{
float k = 32.0;            // softness
float res = 1.0;
float t = 0.1;             // min-t see http://www.iquilezles.org/www/articles/rmshadows/rmshadows.htm
for (int i=0; i<16; i++)
{
float h = DistObjects(ro + rd * t).x;
res = min(res, k*h/t);
t += h;
if (t > 32.0) break;    // max-t
}
return clamp(res, 0.0, 1.0);
}

vec3 GetNormal(vec3 pos)
{
vec3 n;
n.x = DistAll( pos + delta.xyy ).x - DistAll( pos - delta.xyy ).x;
n.y = DistAll( pos + delta.yxy ).x - DistAll( pos - delta.yxy ).x;
n.z = DistAll( pos + delta.yyx ).x - DistAll( pos - delta.yyx ).x;
return normalize(n);
}

vec3 Sky(vec3 rd)
{
vec3 sky = mix( vec3(.8), vec3(0), exp2(-(1.0/max(rd.y, 0.25))*vec3(.6,.8,1.0)) );
float sunAmount = max(dot(rd, lightDir), 0.0);
sky += vec3(1.0,0.6,0.3) * sunAmount * sunAmount * .5 + vec3(1.0,0.6,0.3) * min(pow(sunAmount, 800.0), .3);

return sky;
}

const float WATER_SPEC = 40.0;
const float WATER_SPEC_HARDNESS = 512.0;
const vec3  WATER_COLOR = vec3(0.6,0.9,0.6);
const float WATER_DIFFUSE = 0.05;
const float WATER_ATTENUATION = 0.5;
vec4 Shading(vec3 pos, vec3 ro, vec3 rd, vec3 norm, float mat)
{
if (mat != MATERIAL_SKY)
{
float lighting = max(0.0, dot(norm, lightDir));
vec3 view = normalize(-rd);
vec3 heading = normalize(view + lightDir);

if (mat == MATERIAL_WATER)
{
   float fresnel = pow(1.0 - dot(view, norm), 5.0);
   fresnel = mix(0.0, 1.0, min(1.0, fresnel));
   
   float spec = pow(max(0.0, dot(heading, norm)), WATER_SPEC_HARDNESS);
   vec3 light = (WATER_DIFFUSE * lighting * WATER_COLOR) + (spec * WATER_SPEC * lightColour) * (1.0-fresnel);
   light *= SoftShadow(pos, lightDir);
   
   // perform simple fresnel reflection directly here
   if (fresnel > 0.0) light += Sky(reflect(rd, norm)) * fresnel;

   // attenuation
   vec3 dist = pos - ro;
   float atten = max(1.0 - dot(dist,dist) * 0.001, 0.0);
   light += WATER_COLOR * (pos.y + WATER_ATTENUATION) * 0.20 * atten;

   return vec4(light, 0.0);
}
else if (mat >= MATERIAL_METAL)
{
   float spec = pow(max(0.0, dot(heading, norm)), specularHardness);
   vec3 light = (diffuse * lighting * lightColour) + (spec * specular);
   light *= SoftShadow(pos, lightDir);
   light += CalcAO(pos, norm) * ambientFactor;
   // metal surface reflection coefficient
   return vec4(light, mat - MATERIAL_METAL);
}
}
else return vec4(Sky(rd), 0.0);
}

// Camera function by TekF
// Compute ray fro8m camera parameters
vec3 GetRay(vec3 dir, vec2 pos)
{
pos = pos - 0.5;
pos.x *= resolution.x/resolution.y;

dir = normalize(dir);
vec3 right = normalize(cross(vec3(0.,1.,0.),dir));
vec3 up = normalize(cross(dir,right));

return dir + right*pos.x + up*pos.y;
}

vec2 March(vec3 ro, vec3 rd, inout vec3 p)
{
float t = 0.0;
float d = 0.0;
for (int i=0; i<RAY_DEPTH; i++)
{
p = ro + rd * t;
vec2 dm = DistAll(p);
if (dm.x < DISTANCE_MIN)
{
   return dm;
}
t += dm.x;
if (t >= MAX_DEPTH) break;
}
return vec2(0.0, MATERIAL_SKY);
}

vec2 MarchReflect(vec3 ro, vec3 rd, inout vec3 p)
{
float t = 0.0;
float d = 0.0;
for (int i=0; i<RAY_DEPTH/2; i++)
{
p = ro + rd * t;
vec2 dm = DistAll(p);
if (dm.x < DISTANCE_MIN)
{
   return dm;
}
t += dm.x;
if (t >= MAX_DEPTH) break;
}
return vec2(0.0, MATERIAL_SKY);
}

// Original method by David Hoskins
#define GAMMA 0.8
#define CONTRAST 1.1
#define SATURATION 1.2
#define BRIGHTNESS 1.1
vec3 PostEffects(vec3 rgb, vec2 xy)
{
rgb = pow(rgb, vec3(GAMMA));
rgb = mix(vec3(.5), mix(vec3(dot(vec3(.2125, .7154, .0721), rgb*BRIGHTNESS)), rgb*BRIGHTNESS, SATURATION), CONTRAST);
rgb *= .4+0.5*pow(40.0*xy.x*xy.y*(1.0-xy.x)*(1.0-xy.y), 0.2 );	
return rgb;
}

const float fog = 0.02;
vec3 Fog(vec3 rgb, vec3 rd, float distance)
{
float fogAmount = 1.0 - exp(-distance*fog);
vec3  fogColor = Sky(rd);
return mix(rgb, fogColor, fogAmount);
}

void main()
{
const int ANTIALIAS_SAMPLES = 1;

vec3 cpos = cameraPos;
vec3 lookAt = cameraLookat;
if (rotateWorld)
{
cpos.y = sin(time*0.5)*4.0 + 8.0;
cpos.x = cos(time*.25)*8.0 + 4.0;
lookAt.y = -sin(time*0.5)*4.0 - 8.0;
}

vec4 res = vec4(0.0);
vec2 p;
float d_ang = 2.*PI / float(ANTIALIAS_SAMPLES);
float ang = d_ang * 0.33333;
float r = 0.5;
for (int i = 0; i < ANTIALIAS_SAMPLES; i++)
{
p = vec2((gl_FragCoord.x + cos(ang)*r) / resolution.x, (gl_FragCoord.y + sin(ang)*r) / resolution.y);
vec3 ro = cpos;
vec3 rd = normalize(GetRay(lookAt-cpos, p));
vec3 hit;

vec2 dm = March(ro, rd, hit);
float fogDistance = length(ro - hit);
vec3 fogRd = rd;
vec3 norm = GetNormal(hit);
vec4 shade = Shading(hit, ro, rd, norm, dm.y);
shade.rgb *= 1.0 - shade.a;
if (shade.a != 0.0) 
{
   // ray reflection - shift out again so not immediately hit same point
   rd = reflect(rd, norm);
   ro = hit + (rd * DISTANCE_MIN);
   dm = MarchReflect(ro, rd, hit);
   norm = GetNormal(hit);
   vec4 shade1 = Shading(hit, ro, rd, norm, dm.y);
   if (shade1.a != 0.0)
   {
      shade.rgb += Fog(shade1.rgb * (1.0 - shade1.a), rd, length(ro - hit)) * shade.a * 0.6;
      
      // ray reflection - shift out again so not immediately hit same point
      rd = reflect(rd, norm);
      ro = hit + (rd * DISTANCE_MIN);
      dm = MarchReflect(ro, rd, hit);
      norm = GetNormal(hit);
      shade.rgb += Fog(Shading(hit, ro, rd, norm, dm.y).rgb, rd, length(ro - hit)) * shade.a * 0.4;
   }
   else shade.rgb += Fog(shade1.rgb, rd, length(ro - hit)) * shade.a;
}
res.rgb += Fog(shade.rgb, fogRd, fogDistance);
ang += d_ang;
}
res.rgb /= float(ANTIALIAS_SAMPLES);

gl_FragColor = vec4(PostEffects(res.rgb, p), 1.0);
}
</script>

<script type="text/javascript">
   var requestAnimationFrame = window.requestAnimationFrame || window.webkitRequestAnimationFrame ||
      window.mozRequestAnimationFrame || window.msRequestAnimationFrame ||
      function (c) { window.setTimeout(c, 15) };
   var config = {
      camera: {
         x: 8.0, y: 10.0, z: 6.0
      },
      lookat: {
         x: -8.0, y: 0.0, z: -6.0
      },
      lightDir: {
         x: -10.0, y: 7.0, z: 0.0
      },
      lightColour: {
         r: 1.8, g: 1.5, b: 1.5
      },
      surface: {
         specular: 3.0,
         specularHardness: 16.0,
         diffuse: 0.1,
         ambientFactor: 0.2
      },
      global: {
         rotateWorld: true,
         antialias: "None",// None|Classic
         param: 0.5
      }
   };

   var aspect, gl;
   function init() {
      var pause = false;
      document.addEventListener('keydown', function (e) {
         switch (e.keyCode) {
            case 32: // SPACE
            case 27: // ESC
               pause = !pause;
               break;
         }
      }, false);

      // add GUI controls
      var mobile = (navigator.userAgent.indexOf("Android") !== -1);
      var gui = new dat.GUI();
      var panel = gui.addFolder('Camera Position');
      panel.add(config.camera, "x").min(-160.0).max(160.0).step(0.1);
      panel.add(config.camera, "y").min(-16.0).max(16.0).step(0.1);
      panel.add(config.camera, "z").min(-160.0).max(160.0).step(0.1);
      //panel.open();
      panel = gui.addFolder('Camera LookAt');
      panel.add(config.lookat, "x").min(-160.0).max(160.0).step(0.1);
      panel.add(config.lookat, "y").min(-16.0).max(16.0).step(0.1);
      panel.add(config.lookat, "z").min(-160.0).max(160.0).step(0.1);
      //if (!mobile) panel.open();
      panel = gui.addFolder('Light Direction');
      panel.add(config.lightDir, "x").min(-16.0).max(16.0).step(0.1);
      panel.add(config.lightDir, "y").min(-16.0).max(16.0).step(0.1);
      panel.add(config.lightDir, "z").min(-16.0).max(16.0).step(0.1);
      if (!mobile) panel.open();
      panel = gui.addFolder('Light Colour');
      panel.add(config.lightColour, "r").min(0.0).max(3.0).step(0.1);
      panel.add(config.lightColour, "g").min(0.0).max(3.0).step(0.1);
      panel.add(config.lightColour, "b").min(0.0).max(3.0).step(0.1);
      //if (!mobile) panel.open();
      panel = gui.addFolder('Surface');
      panel.add(config.surface, "specular").min(0).max(64);
      panel.add(config.surface, "specularHardness").min(16).max(1024).step(16);
      panel.add(config.surface, "diffuse").min(0).max(1).step(0.05);
      panel.add(config.surface, "ambientFactor").min(0).max(1).step(0.05);
      //panel.open();
      panel = gui.addFolder('Global');
      //panel.add(config.global, "antialias", ["None", "Classic"]).name("Anti Alias");
      panel.add(config.global, "rotateWorld").name("Rotate World");
      panel.add(config.global, "param").min(0.01).max(5.0).step(0.01);
      if (!mobile) panel.open();

      var stats = new Stats();
      document.body.appendChild(stats.domElement);

      // create webgl context on the canvas element
      var canvas = document.getElementById("canvas");
      aspect = canvas.width / canvas.height;
      try {
         gl = canvas.getContext("experimental-webgl");
      }
      catch (e) {
         document.write("Whoops! No useful WEB-GL impl available. Shame on you and your browser vendor.<br>" + e.message);
         return;
      }
      gl.viewport(0, 0, canvas.width, canvas.height);
      gl.clearColor(0, 0, 0, 1);
      gl.clear(gl.COLOR_BUFFER_BIT);

      // get the vertex and fragment shader source
      var v = document.getElementById("vertex").firstChild.nodeValue;
      var f = document.getElementById("fragment").firstChild.nodeValue;

      // compile and link the shaders
      var vs = gl.createShader(gl.VERTEX_SHADER);
      gl.shaderSource(vs, v);
      gl.compileShader(vs);

      var fs = gl.createShader(gl.FRAGMENT_SHADER);
      gl.shaderSource(fs, f);
      gl.compileShader(fs);

      var program = gl.createProgram();
      gl.attachShader(program, vs);
      gl.attachShader(program, fs);
      gl.linkProgram(program);

      // debug shader compile status
      var error = false;
      if (!gl.getShaderParameter(vs, gl.COMPILE_STATUS)) {
         error = true;
         console.log(gl.getShaderInfoLog(vs));
      }

      if (!gl.getShaderParameter(fs, gl.COMPILE_STATUS)) {
         error = true;
         console.log(gl.getShaderInfoLog(fs));
      }

      if (!gl.getProgramParameter(program, gl.LINK_STATUS)) {
         error = true;
         console.log(gl.getProgramInfoLog(program));
      }
      if (error) return;

      var firstTime = Date.now();
      (f = function () {
         if (!pause) {
            stats.begin();

            // create vertices to fill the canvas with a single quad 
            var vertices = new Float32Array(
               [
                  -1, 1 * aspect, 1, 1 * aspect, 1, -1 * aspect,
                  -1, 1 * aspect, 1, -1 * aspect, -1, -1 * aspect
               ]);

            var vbuffer = gl.createBuffer();
            gl.bindBuffer(gl.ARRAY_BUFFER, vbuffer);
            gl.bufferData(gl.ARRAY_BUFFER, vertices, gl.STATIC_DRAW);

            var triCount = 2,
               numItems = vertices.length / triCount;

            gl.useProgram(program);

            var time = (Date.now() - firstTime) / 1000.0;
            program.time = gl.getUniformLocation(program, "time");
            gl.uniform1f(program.time, time);

            program.resolution = gl.getUniformLocation(program, "resolution");
            gl.uniform2f(program.resolution, canvas.width, canvas.height);

            program.cameraPos = gl.getUniformLocation(program, "cameraPos");
            gl.uniform3f(program.cameraPos, config.camera.x, config.camera.y, config.camera.z);

            program.cameraLookat = gl.getUniformLocation(program, "cameraLookat");
            gl.uniform3f(program.cameraLookat, config.lookat.x, config.lookat.y, config.lookat.z);

            program.lightDir = gl.getUniformLocation(program, "lightDir");
            // pre normalise light dir
            var x = config.lightDir.x, y = config.lightDir.y, z = config.lightDir.z;
            var len = x * x + y * y + z * z;
            len = 1.0 / Math.sqrt(len);
            gl.uniform3f(program.lightDir, config.lightDir.x * len, config.lightDir.y * len, config.lightDir.z * len);

            program.lightColour = gl.getUniformLocation(program, "lightColour");
            gl.uniform3f(program.lightColour, config.lightColour.r, config.lightColour.g, config.lightColour.b);

            program.specular = gl.getUniformLocation(program, "specular");
            gl.uniform1f(program.specular, config.surface.specular);

            program.specularHardness = gl.getUniformLocation(program, "specularHardness");
            gl.uniform1f(program.specularHardness, config.surface.specularHardness);

            program.diffuse = gl.getUniformLocation(program, "diffuse");
            gl.uniform3f(program.diffuse, config.surface.diffuse, config.surface.diffuse, config.surface.diffuse);

            program.ambientFactor = gl.getUniformLocation(program, "ambientFactor");
            gl.uniform1f(program.ambientFactor, config.surface.ambientFactor);

            program.rotateWorld = gl.getUniformLocation(program, "rotateWorld");
            gl.uniform1f(program.rotateWorld, config.global.rotateWorld);

            //program.antialias = gl.getUniformLocation(program, "antialias");
            //gl.uniform1f(program.antialias, (config.global.antialias === "Classic"));

            program.param = gl.getUniformLocation(program, "param");
            gl.uniform1f(program.param, config.global.param);

            program.aVertexPosition = gl.getAttribLocation(program, "aVertexPosition");
            gl.enableVertexAttribArray(program.aVertexPosition);
            gl.vertexAttribPointer(program.aVertexPosition, triCount, gl.FLOAT, false, 0, 0);

            gl.drawArrays(gl.TRIANGLES, 0, numItems);

            stats.end();
            //pause = true;
         }
         requestAnimationFrame(f);
      })();
   }
</script>
<script src="js/utils.js"></script>

</html>