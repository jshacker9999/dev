<!DOCTYPE html>
<html>

<head>
   <meta charset="UTF-8">
   <meta name="viewport" content="width=device-width, initial-scale=1.0">
   <meta http-equiv="X-UA-Compatible" content="ie=edge">
   <title>Distance Field - Lighting, shadows, AO and camera animation. High-resolution render with anti-aliasing and
      depth-of-field effect.</title>
   <link rel="stylesheet" href="css/reset.css">
   <link rel="stylesheet" href="css/page.css">
   <link href='http://fonts.googleapis.com/css?family=Open+Sans:400,300,600' rel='stylesheet' type='text/css'>
</head>

<body onload="init()">
   <canvas id="canvas" width="768" height="512"></canvas>
   <a href="#" id="fullscreen">Toggle Full Screen</a>

</body>
<script src="js/dat.gui.min.js"></script>
<script src="js/stats.min.js"></script>
<script id="vertex" type="x-shader">
attribute vec2 aVertexPosition;

void main()
{
   gl_Position = vec4(aVertexPosition, 0.0, 1.0);
}
      </script>
<script id="fragment" type="x-shader">
#ifdef GL_ES
precision highp float;
#endif

uniform float time;
uniform vec2 resolution;
uniform vec3 cameraPos;
uniform vec3 cameraLookat;
uniform vec3 lightDir;
uniform vec3 lightColour;
uniform float specular;
uniform float specularHardness;
uniform vec3 diffuse;
uniform float ambientFactor;
uniform bool ao;
uniform bool shadows;
uniform bool postEffects;
uniform bool rotateWorld;
uniform bool moveCamera;
uniform bool antialias;

#define PI 3.14159265
#define GAMMA 0.8
#define CONTRAST 1.1
#define SATURATION 1.3
#define BRIGHTNESS 1.3
#define AO_SAMPLES 5
#define RAY_DEPTH 256
#define MAX_DEPTH 100.0
#define SHADOW_RAY_DEPTH 32
#define DISTANCE_MIN 0.001

const vec2 delta = vec2(0.001, 0.);


vec3 RotateZ(vec3 p, float a)
{
   float c,s;
   vec3 q=p;
   c = cos(a);
   s = sin(a);
   p.x = c * q.x - s * q.y;
   p.y = s * q.x + c * q.y;
   return p;
}

// polynomial smooth functions (smoothing factor k = ~0.1)
float smax(float a, float b, float k)
{
   float h = clamp(0.5+0.5*(b-a)/k, 0.0, 1.0);
   return mix(a, b, h) - k*h*h;
}

float Sphere(vec3 p, float s)
{
   return length(p)-s;
}

float HexPrism(vec3 p, vec2 h)
{
   vec3 q = abs(p);
   return max(q.z-h.y,max(q.x+q.y*0.57735,q.y*1.1547)-h.x);
}

float Plane(vec3 p, vec3 n)
{
   return dot(p, n);
}

float ReplicateXZ(vec3 p, vec3 c)
{
   vec3 q = vec3(mod(p.x, c.x) - 0.5 * c.x, p.y, mod(p.z, c.z) - 0.5 * c.z);
   return
      min(
         // ground plane - offset in the Y axis
         Plane(q-vec3(0.,-0.85,0.), vec3(0.,1.,0.)),
         // smoothly subtract spheres from our hexprism shape
         smax(
            // subtract one hex prism from another to make a hollow shape
            max(
               HexPrism(q, vec2(1.0,0.5)),
               -HexPrism(q-vec3(0.,0.,0.25), vec2(0.8,1.0))
            ),
            // combine 6 offset spheres to make holes in the hollow hexprism shape
            -min(
               Sphere(q-vec3(0.,0.85,0.), 0.333),
               min(
                  Sphere(q-vec3(0.,-0.85,0.), 0.333),
                  min(
                     Sphere(q-vec3(0.75,0.45,0.), 0.333),
                     min(
                        Sphere(q-vec3(0.75,-0.45,0.), 0.333),
                        min(
                           Sphere(q-vec3(-0.75,0.45,0.), 0.333),
                           Sphere(q-vec3(-0.75,-0.45,0.), 0.333)
                        )
                     )
                  )
               )
            ),0.05
         )
      );
}

// This should return continuous positive values when outside and negative values inside,
// which roughly indicate the distance of the nearest surface.
float Dist(vec3 pos)
{
   if (rotateWorld) pos = RotateZ(pos, sin(time)*0.5);
   return ReplicateXZ(pos, vec3(4.,0.,4.));
}

// Based on original by IQ
float CalcAO(vec3 p, vec3 n)
{
   float r = 0.0;
   float w = 1.0;
   for (int i=1; i<=AO_SAMPLES; i++)
   {
      float d0 = float(i) * 0.3;
      r += w * (d0 - Dist(p + n * d0));
      w *= 0.5;
   }
   return 1.0 - r;
}

// Based on original code by IQ
float SoftShadow(vec3 ro, vec3 rd, float k)
{
   float res = 1.0;
   float t = 0.05;          // min-t see http://www.iquilezles.org/www/articles/rmshadows/rmshadows.htm
   for (int i=0; i<SHADOW_RAY_DEPTH; i++)
   {
      if (t < 20.0)  // max-t
      {
         float h = Dist(ro + rd * t);
         res = min(res, k*h/t);
         t += h;
      }
   }
   return clamp(res, 0.0, 1.0);
}

vec3 GetNormal(vec3 pos)
{
   vec3 n;
   n.x = Dist( pos + delta.xyy ) - Dist( pos - delta.xyy );
   n.y = Dist( pos + delta.yxy ) - Dist( pos - delta.yxy );
   n.z = Dist( pos + delta.yyx ) - Dist( pos - delta.yyx );
   
   return normalize(n);
}

// Original method by David Hoskins
vec3 Sky(in vec3 rd)
{
   float sunAmount = max(dot(rd, lightDir), 0.0);
   float v = pow(1.0 - max(rd.y,0.0),6.);
   vec3 sky = mix(vec3(.1, .2, .3), vec3(.32, .32, .32), v);
   sky += lightColour * sunAmount * sunAmount * .25 + lightColour * min(pow(sunAmount, 800.0)*1.5, .3);
   return sky;
}

// Fog routine - original by IQ
vec3 Fog(vec3 rgb, vec3 rd, float distance)   // camera to point distance
{
   const float b = 0.185;  //0.035;
   float fogAmount = 1.0 - exp(-distance*b*b);
   vec3 fogColor = Sky(rd);
   return mix(rgb, fogColor, fogAmount);
}

// Based on a shading method by Ben Weston. Added AO and SoftShadows to original.
vec4 Shading(vec3 pos, vec3 rd, vec3 norm, vec3 ro)
{
   vec3 heading = normalize(-rd + lightDir);
   float spec = pow(max(0.0, dot(heading, norm)), specularHardness);
   
   float sun = clamp(dot(norm, lightDir), 0.0, 1.0);
   vec3 light = (sun * lightColour * diffuse) + (spec * specular * lightColour);
   
   if (shadows) light *= SoftShadow(pos, lightDir, 32.0);
   if (ao) light += CalcAO(pos, norm) * ambientFactor;
   
   light = Fog(light, rd, length(ro-pos));
   
   return vec4(light, 1.0);
}

// Original method by David Hoskins
vec3 PostEffects(vec3 rgb, vec2 xy)
{
	rgb = pow(rgb, vec3(GAMMA));
	rgb = mix(vec3(.5), mix(vec3(dot(vec3(.2125, .7154, .0721), rgb*BRIGHTNESS)), rgb*BRIGHTNESS, SATURATION), CONTRAST);
	rgb *= .4+0.5*pow(40.0*xy.x*xy.y*(1.0-xy.x)*(1.0-xy.y), 0.2 );	
	return rgb;
}

// Camera function by TekF
// Compute ray from camera parameters
vec3 GetRay(vec3 dir, vec2 pos)
{
   pos = pos - 0.5;
   pos.x *= resolution.x/resolution.y;
   
   dir = normalize(dir);
   vec3 right = normalize(cross(vec3(0.,1.,0.),dir));
   vec3 up = normalize(cross(dir,right));
   
   return dir + right*pos.x + up*pos.y;
}

vec4 March(vec3 ro, vec3 rd)
{
   float t = 0.0;
   float d = 1.0;
   for (int i=0; i<RAY_DEPTH; i++)
   {
      vec3 p = ro + rd * t;
      d = Dist(p);
      if (abs(d) < DISTANCE_MIN)
      {
         return vec4(p, 1.0);
      }
      t += d;
      if (t >= MAX_DEPTH) break;
   }
   return vec4(0.0);
}

void main()
{
   const int ANTIALIAS_SAMPLES = 4;
   
   vec4 res = vec4(0.0);
   
   vec3 off = vec3(0.0);
   if (moveCamera) off.z -= time*10.0;
   
   if (antialias)
   {
      vec2 p;
      float d_ang = 2.*PI / float(ANTIALIAS_SAMPLES);
      float ang = d_ang * 0.33333;
      float r = 0.3;
      for (int i = 0; i < ANTIALIAS_SAMPLES; i++)
      {
         p = vec2((gl_FragCoord.x + cos(ang)*r) / resolution.x, (gl_FragCoord.y + sin(ang)*r) / resolution.y);
         vec3 ro = cameraPos + off;
         vec3 rd = normalize(GetRay(cameraLookat-cameraPos, p));
         vec4 _res = March(ro, rd);
         if (_res.a == 1.0) res.rgb += Shading(_res.rgb, rd, GetNormal(_res.rgb), ro).rgb;
         else res.rgb += Sky(rd);
         ang += d_ang;
      }
      res.rgb /= float(ANTIALIAS_SAMPLES);
      if (postEffects) res.rgb = PostEffects(res.rgb, p);
   }
   else
   {
      vec2 p = gl_FragCoord.xy / resolution.xy;
      vec3 ro = cameraPos + off;
      vec3 rd = normalize(GetRay((cameraLookat-off)-cameraPos+off, p));
      
      res = March(ro, rd);
      if (res.a == 1.0) res.rgb = Shading(res.rgb, rd, GetNormal(res.rgb), ro).rgb;
      else res.rgb = Sky(rd);
      if (postEffects) res.rgb = PostEffects(res.rgb, p);
   }
   
   gl_FragColor = vec4(res.rgb, 1.0);
}
      </script>

<script type="text/javascript">
   var requestAnimationFrame = window.requestAnimationFrame || window.webkitRequestAnimationFrame ||
      window.mozRequestAnimationFrame || window.msRequestAnimationFrame ||
      function (c) { window.setTimeout(c, 15) };
   var config = {
      camera: {
         x: 9.0, y: 4.5, z: 0.0
      },
      lookat: {
         x: 0.0, y: -10.0, z: -100.0
      },
      lightDir: {
         x: -1.4, y: 0.8, z: -1.0
      },
      lightColour: {
         r: 3.0, g: 1.4, b: 0.3
      },
      surface: {
         specular: 64.0,
         specularHardness: 256.0,
         diffuse: 0.4,
         ambientFactor: 0.2
      },
      global: {
         ao: true,
         shadows: true,
         postEffects: true,
         antialias: "None",// None|Classic
         rotateWorld: true,
         moveCamera: true
      }
   };

   var pause = false;
   var stats = new Stats();
   var aspect, gl;
   function init() {
      document.addEventListener('keydown', function (e) {
         switch (e.keyCode) {
            case 27: // ESC
               pause = !pause;
               break;
         }
      }, false);

      // add GUI controls
      var mobile = (navigator.userAgent.indexOf("Android") !== -1);
      var gui = new dat.GUI();
      var panel = gui.addFolder('Camera Position');
      panel.add(config.camera, "x").min(-256).max(256).step(0.1);
      panel.add(config.camera, "y").min(-100).max(100).step(0.1);
      panel.add(config.camera, "z").min(-100).max(100).step(0.1);
      //panel.open();
      panel = gui.addFolder('Camera LookAt');
      panel.add(config.lookat, "x").min(-256).max(256).step(0.1);
      panel.add(config.lookat, "y").min(-100).max(100).step(0.1);
      panel.add(config.lookat, "z").min(-100).max(100).step(0.1);
      if (!mobile) panel.open();
      panel = gui.addFolder('Light Direction');
      panel.add(config.lightDir, "x").min(-10).max(10).step(0.1);
      panel.add(config.lightDir, "y").min(-10).max(10).step(0.1);
      panel.add(config.lightDir, "z").min(-10).max(10).step(0.1);
      if (!mobile) panel.open();
      panel = gui.addFolder('Light Colour');
      panel.add(config.lightColour, "r").min(0).max(3).step(0.1);
      panel.add(config.lightColour, "g").min(0).max(3).step(0.1);
      panel.add(config.lightColour, "b").min(0).max(3).step(0.1);
      if (!mobile) panel.open();
      panel = gui.addFolder('Surface');
      panel.add(config.surface, "specular").min(0).max(64);
      panel.add(config.surface, "specularHardness").min(16).max(1024).step(16);
      panel.add(config.surface, "diffuse").min(0).max(1).step(0.05);
      panel.add(config.surface, "ambientFactor").min(0).max(1).step(0.05);
      //panel.open();
      panel = gui.addFolder('Global');
      panel.add(config.global, "antialias", ["None", "Classic"]).name("Anti Alias");
      panel.add(config.global, "shadows").name("Shadows");
      panel.add(config.global, "ao").name("Ambient Occlusion");
      panel.add(config.global, "postEffects").name("Post Effects");
      panel.add(config.global, "rotateWorld").name("Rotate World");
      panel.add(config.global, "moveCamera").name("Move Camera");
      if (!mobile) panel.open();

      document.body.appendChild(stats.domElement);

      // create webgl context on the canvas element
      var canvas = document.getElementById("canvas");
      aspect = canvas.width / canvas.height;
      try {
         gl = canvas.getContext("experimental-webgl");
      }
      catch (e) {
         document.write("Whoops! No useful WEB-GL impl available. Shame on you and your browser vendor.<br>" + e.message);
         return;
      }
      gl.viewport(0, 0, canvas.width, canvas.height);
      gl.clearColor(0, 0, 0, 1);
      gl.clear(gl.COLOR_BUFFER_BIT);

      // get the vertex and fragment shader source
      var v = document.getElementById("vertex").firstChild.nodeValue;
      var f = document.getElementById("fragment").firstChild.nodeValue;

      // compile and link the shaders
      var vs = gl.createShader(gl.VERTEX_SHADER);
      gl.shaderSource(vs, v);
      gl.compileShader(vs);

      var fs = gl.createShader(gl.FRAGMENT_SHADER);
      gl.shaderSource(fs, f);
      gl.compileShader(fs);

      var program = gl.createProgram();
      gl.attachShader(program, vs);
      gl.attachShader(program, fs);
      gl.linkProgram(program);

      // debug shader compile status
      var error = false;
      if (!gl.getShaderParameter(vs, gl.COMPILE_STATUS)) {
         error = true;
         console.log(gl.getShaderInfoLog(vs));
      }

      if (!gl.getShaderParameter(fs, gl.COMPILE_STATUS)) {
         error = true;
         console.log(gl.getShaderInfoLog(fs));
      }

      if (!gl.getProgramParameter(program, gl.LINK_STATUS)) {
         error = true;
         console.log(gl.getProgramInfoLog(program));
      }
      if (error) return;

      var firstTime = Date.now();
      (f = function () {
         if (!pause) {
            stats.begin();

            // create vertices to fill the canvas with a single quad 
            var vertices = new Float32Array(
               [
                  -1, 1 * aspect, 1, 1 * aspect, 1, -1 * aspect,
                  -1, 1 * aspect, 1, -1 * aspect, -1, -1 * aspect
               ]);

            var vbuffer = gl.createBuffer();
            gl.bindBuffer(gl.ARRAY_BUFFER, vbuffer);
            gl.bufferData(gl.ARRAY_BUFFER, vertices, gl.STATIC_DRAW);

            var triCount = 2,
               numItems = vertices.length / triCount;

            gl.useProgram(program);

            var time = (Date.now() - firstTime) / 1000.0;
            program.time = gl.getUniformLocation(program, "time");
            gl.uniform1f(program.time, time);

            program.resolution = gl.getUniformLocation(program, "resolution");
            gl.uniform2f(program.resolution, canvas.width, canvas.height);

            program.cameraPos = gl.getUniformLocation(program, "cameraPos");
            gl.uniform3f(program.cameraPos, config.camera.x, config.camera.y, config.camera.z);

            program.cameraLookat = gl.getUniformLocation(program, "cameraLookat");
            gl.uniform3f(program.cameraLookat, config.lookat.x, config.lookat.y, config.lookat.z);

            program.lightDir = gl.getUniformLocation(program, "lightDir");
            // pre normalise light dir
            var x = config.lightDir.x, y = config.lightDir.y, z = config.lightDir.z;
            var len = x * x + y * y + z * z;
            len = 1.0 / Math.sqrt(len);
            gl.uniform3f(program.lightDir, config.lightDir.x * len, config.lightDir.y * len, config.lightDir.z * len);

            program.lightColour = gl.getUniformLocation(program, "lightColour");
            gl.uniform3f(program.lightColour, config.lightColour.r, config.lightColour.g, config.lightColour.b);

            program.specular = gl.getUniformLocation(program, "specular");
            gl.uniform1f(program.specular, config.surface.specular);

            program.specularHardness = gl.getUniformLocation(program, "specularHardness");
            gl.uniform1f(program.specularHardness, config.surface.specularHardness);

            program.diffuse = gl.getUniformLocation(program, "diffuse");
            gl.uniform3f(program.diffuse, config.surface.diffuse, config.surface.diffuse, config.surface.diffuse);

            program.ambientFactor = gl.getUniformLocation(program, "ambientFactor");
            gl.uniform1f(program.ambientFactor, config.surface.ambientFactor);

            program.rotateWorld = gl.getUniformLocation(program, "rotateWorld");
            gl.uniform1f(program.rotateWorld, config.global.rotateWorld);

            program.moveCamera = gl.getUniformLocation(program, "moveCamera");
            gl.uniform1f(program.moveCamera, config.global.moveCamera);

            program.postEffects = gl.getUniformLocation(program, "postEffects");
            gl.uniform1f(program.postEffects, config.global.postEffects);

            program.ao = gl.getUniformLocation(program, "ao");
            gl.uniform1f(program.ao, config.global.ao);

            program.shadows = gl.getUniformLocation(program, "shadows");
            gl.uniform1f(program.shadows, config.global.shadows);

            program.antialias = gl.getUniformLocation(program, "antialias");
            gl.uniform1f(program.antialias, (config.global.antialias === "Classic"));

            program.aVertexPosition = gl.getAttribLocation(program, "aVertexPosition");
            gl.enableVertexAttribArray(program.aVertexPosition);
            gl.vertexAttribPointer(program.aVertexPosition, triCount, gl.FLOAT, false, 0, 0);

            gl.drawArrays(gl.TRIANGLES, 0, numItems);

            stats.end();
         }
         requestAnimationFrame(f);
      })();
   }
</script>
<script src="js/utils.js"></script>

</html>