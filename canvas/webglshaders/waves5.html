<!DOCTYPE html>
<html>

<head>
   <meta charset="UTF-8">
   <meta name="viewport" content="width=device-width, initial-scale=1.0">
   <meta http-equiv="X-UA-Compatible" content="ie=edge">
   <title>Ibiza Sunset</title>
   <link rel="stylesheet" href="css/reset.css">
   <link rel="stylesheet" href="css/page.css">
   <link href='http://fonts.googleapis.com/css?family=Open+Sans:400,300,600' rel='stylesheet' type='text/css'>
</head>

<body onload="init()">
   <canvas id="canvas" width="768" height="512"></canvas>
   <a href="#" id="fullscreen">Toggle Full Screen</a>

</body>
<script src="js/utils.js"></script>
<script src="js/dat.gui.min.js"></script>
<script src="js/stats.min.js"></script>
<script id="vertex" type="x-shader">
attribute vec2 aVertexPosition;

void main()
{
   gl_Position = vec4(aVertexPosition, 0.0, 1.0);
}
      </script>
<script id="fragment" type="x-shader">
#ifdef GL_ES
precision highp float;
#endif

uniform float time;
uniform vec2 resolution;
uniform vec3 cameraPos;
uniform vec3 cameraLookat;
uniform vec3 sunDir;
uniform vec3 sunColour;
uniform float specular;
uniform float specularHardness;
uniform float fog;
uniform vec3 diffuse;
uniform bool postEffects;
uniform bool moveCamera;
uniform bool reflections;
uniform float attenDepth;
uniform float attenScale;
uniform float floorDepth;
uniform float param;
uniform sampler2D sandTexture;

//#define AA

#define GAMMA 0.8
#define CONTRAST 1.1
#define SATURATION 1.2
#define BRIGHTNESS 1.2
#ifndef AA
#define NOISE_PASSES 5
#define RAY_DEPTH 256
#define MAX_DEPTH 200.0
#define DISTANCE_MIN 0.003
#else
#define ANTIALIAS_SAMPLES 8
#define NOISE_PASSES 8
#define RAY_DEPTH 256
#define MAX_DEPTH 200.0
#define DISTANCE_MIN 0.003
#endif
#define PI 3.14159265
#define TSCALE 0.25

#define MATERIAL_SKY 0.0
#define MATERIAL_WATER 1.0
#define MATERIAL_BALL 2.0
#define MATERIAL_FLOOR 3.0

const vec2 delta = vec2(DISTANCE_MIN, 0.);

float Hash(in float n)
{
    return fract(sin(n)*43758.5453123);
}

float Noise(in vec2 x)
{
    vec2 p = floor(x);
    vec2 f = fract(x);
    f = f*f*(3.0-2.0*f);
    float n = p.x + p.y*57.0;
    float res = mix(mix( Hash(n+  0.0), Hash(n+  1.0),f.x),
                    mix( Hash(n+ 57.0), Hash(n+ 58.0),f.x),f.y);
    return res;
}

//	FAST32_hash
//	A very fast hashing function.  Requires 32bit support.
//	http://briansharpe.wordpress.com/2011/11/15/a-fast-and-simple-32bit-floating-point-hash-function/
void FAST32_hash_2D( vec2 gridcell, out vec4 hash_0, out vec4 hash_1 )
{
   // gridcell is assumed to be an integer coordinate
   const vec2 OFFSET = vec2( 26.0, 161.0 );
   const float DOMAIN = 71.0;
   const vec2 SOMELARGEFLOATS = vec2( 951.135664, 642.949883 );
   vec4 P = vec4( gridcell.xy, gridcell.xy + 1.0 );
   P = P - floor(P * ( 1.0 / DOMAIN )) * DOMAIN;
   P += OFFSET.xyxy;
   P *= P;
   P = P.xzxz * P.yyww;
   hash_0 = fract( P * ( 1.0 / SOMELARGEFLOATS.x ) );
   hash_1 = fract( P * ( 1.0 / SOMELARGEFLOATS.y ) );
}

vec2 Interpolation_C2( vec2 x ) { return x * x * x * (x * (x * 6.0 - 15.0) + 10.0); }

//	Perlin Noise 2D  ( gradient noise )
//	Return value range of -1.0->1.0
//	http://briansharpe.files.wordpress.com/2011/11/perlinsample.jpg
float Perlin2D( vec2 P )
{
    //	establish our grid cell and unit position
    vec2 Pi = floor(P);
    vec4 Pf_Pfmin1 = P.xyxy - vec4( Pi, Pi + 1.0 );

    //	calculate the hash.
    vec4 hash_x, hash_y;
    FAST32_hash_2D( Pi, hash_x, hash_y );

    //	calculate the gradient results
    vec4 grad_x = hash_x - 0.49999;
    vec4 grad_y = hash_y - 0.49999;
    vec4 grad_results = inversesqrt( grad_x * grad_x + grad_y * grad_y ) * ( grad_x * Pf_Pfmin1.xzxz + grad_y * Pf_Pfmin1.yyww );

    //	Classic Perlin Interpolation
    grad_results *= 1.4142135623730950488016887242097;
    vec2 blend = Interpolation_C2( Pf_Pfmin1.xy );
    vec4 blend2 = vec4( blend, vec2( 1.0 - blend ) );
    return dot( grad_results, blend2.zxzx * blend2.wwyy );
}

// Octave transform matrix from Alexander Alekseev aka TDM 
mat2 octave_m = mat2(1.8,1.2,-1.2,1.8);

// FBM Noise - mixing Value noise and Perlin Noise - also ridged turbulence at smaller octaves
float FractalNoise(in vec2 xy)
{
   float m = 1.00;
   float w = 0.5;
   float f = 0.0;
   for (int i = 0; i < NOISE_PASSES; i++)
   {
      f += Noise(xy.xy+time*0.755) * m * 0.15;
      if (i < NOISE_PASSES-2)
      {
         f += Perlin2D(xy.yx-time*0.533) * w * 0.1;
      }
      else
      {
         // ridged turbulence at smaller scales - moves 4x faster
         f += abs(Noise(xy.yx-time*1.332) * w * 0.15);
      }
      w *= 0.45;
      m *= 0.35;
      xy *= octave_m;
   }
   return f * (abs(sin(1.0-time * 0.025)) * 0.5 + 0.5);  // modulate overall noise
}

vec2 ball(vec3 pos, vec3 ballPos) {
   return vec2(length(pos-ballPos)-0.75, MATERIAL_BALL);
}

vec2 min2(vec2 a, vec2 b) { return a.x < b.x ? a : b; }

vec2 DistSurface(vec3 pos, vec3 ballPos)
{
   return min2(
      vec2(dot(pos-vec3(0.0,-FractalNoise(pos.xz),0.0), vec3(0.,1.,0.)), MATERIAL_WATER),
      ball(pos, ballPos)
   );
}

vec2 DistFloor(vec3 pos, vec3 ballPos)
{
   return min2(
      vec2(dot(pos-vec3(0.0,-floorDepth,0.0), vec3(0.,1.,0.)), MATERIAL_FLOOR),
      ball(pos, ballPos)
   );
}

vec3 GetNormalSurface(vec3 pos, vec3 ballPos)
{
   vec3 n;
   n.x = DistSurface( pos + delta.xyy, ballPos ).x - DistSurface( pos - delta.xyy, ballPos ).x;
   n.y = DistSurface( pos + delta.yxy, ballPos ).x - DistSurface( pos - delta.yxy, ballPos ).x;
   n.z = DistSurface( pos + delta.yyx, ballPos ).x - DistSurface( pos - delta.yyx, ballPos ).x;
   
   return normalize(n);
}

vec3 GetNormalFloor(vec3 pos, vec3 ballPos)
{
   vec3 n;
   n.x = DistFloor( pos + delta.xyy, ballPos ).x - DistFloor( pos - delta.xyy, ballPos ).x;
   n.y = DistFloor( pos + delta.yxy, ballPos ).x - DistFloor( pos - delta.yxy, ballPos ).x;
   n.z = DistFloor( pos + delta.yyx, ballPos ).x - DistFloor( pos - delta.yyx, ballPos ).x;
   
   return normalize(n);
}

// Original method by David Hoskins
vec3 Sky(in vec3 rd)
{
   float sunAmount = max(dot(rd, sunDir), 0.0);
   float v = pow(1.0 - max(rd.y,0.0), 6.0);
   vec3 sky = mix(vec3(0.9,0.35,0.1), vec3(0.5,0.20,0.05), v);
   sky += sunColour * sunAmount * sunAmount * sunAmount * 0.3 + min(pow(sunAmount, 1600.0)*8.0, 0.8);
   
   return clamp(sky, 0.0, 1.0);
}

// Fog routine - original by IQ
vec3 Fog(vec3 rgb, vec3 rd, float distance)
{
   float fogAmount = 1.0 - exp(-distance*fog);
   vec3  fogColor = Sky(rd);
   return mix(rgb, fogColor, fogAmount);
}

const vec3 WATER_COLOR = vec3(0.6,0.85,0.85);
const vec3 SPECULAR_COLOR = vec3(0.9,0.5,0.15);
const vec3 BALL_COLOR = vec3(1.0,1.0,0.0);
const float WATER_DIFFUSE = 0.05;

vec3 CalcBallColour(vec3 pos, vec3 ballPos)
{
   vec3 ballColor = BALL_COLOR;
   ballColor = mix( vec3(1.,0.0,.0), ballColor, smoothstep( .0, .05, cos(atan(pos.x-ballPos.x, pos.z-ballPos.z)*2.0+1.74) ) );
   ballColor = mix( vec3(0.,.0,1.0), ballColor, 1.0 - smoothstep( .0, .05, cos(atan(pos.x-ballPos.x, pos.z-ballPos.z)*1.74)) );
   ballColor = mix( vec3(1.0,1.0,1.0), ballColor, 1.0 - smoothstep( .0, 0.01, abs(pos.y-ballPos.y)-0.73 ) );
   return ballColor;
}

vec4 ShadingSurface(vec3 pos, vec3 rd, vec3 norm, vec3 ro, float m, float shadow, vec3 ballPos)
{
   float lighting = max(0.0, dot(norm, sunDir));
   vec3 view = normalize(-rd);
   vec3 heading = normalize(view + sunDir);
   float spec = pow(max(0.0, dot(heading, norm)), specularHardness);

   if (m == MATERIAL_WATER)
   {
      float fresnel = 0.0;
      if (reflections)
      {
         fresnel = pow(1.0 - dot(view, norm), 5.0);
         fresnel = mix(0.0, 1.0, min(1.0, fresnel));
      }
      
      vec3 light = (WATER_DIFFUSE * lighting * WATER_COLOR) + (spec * specular * SPECULAR_COLOR) * (1.0-fresnel);
      
      // NOTE: fresnel reflection applied now after refraction step - returned in alpha component
      
      // attenuation
      //vec3 dist = pos - ro;
      //float atten = max(1.0 - dot(dist,dist) * 0.0001, 0.0);
      //light += WATER_COLOR * (pos.y - attenDepth) * attenScale * atten;
      // from simple shadow light trace test
      light *= shadow;
      return vec4(light, fresnel);
   }
   else if (m == MATERIAL_BALL)
   {
      vec3 ballColor = CalcBallColour(pos, ballPos);
      vec3 light = (diffuse * lighting * ballColor) + (spec * specular * SPECULAR_COLOR);

      // indirect bounced sunlight
      float ind = clamp(dot(norm, normalize(sunDir * vec3(-1.0,0.0,-1.0))), 0.0, 1.0);
      light += ind * sunColour * ballColor * 0.05;

      // sky light
      float sky = clamp(0.5 + 0.5 * norm.y, 0.0, 1.0);
      light += sky * vec3(0.28, 0.25, 0.22) * ballColor * 0.1;
      return vec4(light, 0.0);
   }
}

vec3 ShadingFloor(vec3 pos, vec3 rd, vec3 norm, vec3 ro, float m, vec3 ballPos)
{
   vec3 light;
   if (m == MATERIAL_FLOOR)
   {
      vec3 colour = texture2D(sandTexture,pos.xz).rgb;
      light = colour * max(0.0, dot(norm, sunDir));
   }
   else if (m == MATERIAL_BALL)
   {
      vec3 view = normalize(-rd);
      vec3 heading = normalize(view + sunDir);
      light = sunColour * max(0.0, dot(norm, sunDir));
      light = (diffuse * light * CalcBallColour(pos, ballPos));
   }
   return light;
}

// Original method by David Hoskins
vec3 PostEffects(vec3 rgb, vec2 xy)
{
   rgb = pow(rgb, vec3(GAMMA));
   rgb = mix(vec3(.5), mix(vec3(dot(vec3(.2125, .7154, .0721), rgb*BRIGHTNESS)), rgb*BRIGHTNESS, SATURATION), CONTRAST);
   rgb *= .4+0.5*pow(40.0*xy.x*xy.y*(1.0-xy.x)*(1.0-xy.y), 0.2 ); 
   return rgb;
}

// Camera function by TekF
// Compute ray from camera parameters
vec3 GetRay(vec3 dir, vec2 pos)
{
   pos = pos - 0.5;
   pos.x *= resolution.x/resolution.y;
   
   dir = normalize(dir);
   vec3 right = normalize(cross(vec3(0.,1.,0.),dir));
   vec3 up = normalize(cross(dir,right));
   
   return dir + right*pos.x + up*pos.y;
}

float March(vec3 ro, vec3 rd, vec3 ballPos)
{
   float t = DISTANCE_MIN;
   for (int i=0; i<32; i++)
   {
      vec3 p = ro + rd * t;
      vec2 dm = ball(p, ballPos);
      if (dm.x < DISTANCE_MIN)
      {
         return dm.x;
      }
      t += dm.x;
      if (t >= 3.0) break;
   }
   return 0.0;
}

vec2 MarchSurface(vec3 ro, vec3 rd, inout vec3 p, vec3 ballPos)
{
   float t = 0.0;
   for (int i=0; i<RAY_DEPTH; i++)
   {
      p = ro + rd * t;
      vec2 dm = DistSurface(p, ballPos);
      if (dm.x < DISTANCE_MIN)
      {
         return dm;
      }
      t += dm.x;
      if (t >= MAX_DEPTH) break;
   }
   return vec2(0.0);
}

vec2 MarchFloor(vec3 ro, vec3 rd, inout vec3 p, vec3 ballPos)
{
   float t = 0.0;
   for (int i=0; i<128; i++)
   {
      p = ro + rd * t;
      vec2 dm = DistFloor(p, ballPos);
      if (dm.x < DISTANCE_MIN)
      {
         return dm;
      }
      t += dm.x;
      if (t >= MAX_DEPTH) break;
   }
   return vec2(0.0);
}

void main()
{
   vec3 off = vec3(0.0);
   if (moveCamera) off.z -= time*0.25;
   vec4 res = vec4(0.0);

   // precal ball position
   vec3 ballPos = vec3(sin(time*0.5)*0.1, FractalNoise(vec2(0.0)), cos(time*0.3)*0.2);

#ifndef AA
   vec2 p = gl_FragCoord.xy / resolution.xy;
   vec3 ro = cameraPos + off;
   vec3 rd = normalize(GetRay((cameraLookat-off)-cameraPos+off, p));
   
   vec3 hit;
   vec2 dm = MarchSurface(ro, rd, hit, ballPos);
   float fogDistance = length(ro-hit);
   if (dm.y != MATERIAL_SKY)
   {
      // fast march towards the light - do we hit anything? if so the water is in shadow
      float shadow = March(hit, sunDir, ballPos);
      vec3 normal = GetNormalSurface(hit, ballPos);
      res = ShadingSurface(hit, rd, normal, ro, dm.y, shadow > 0.0 ? 0.0 : 1.0, ballPos);
      if (dm.y == MATERIAL_WATER)
      {
         // refract ray
         float ndotr = dot(rd, normal);
         vec3 nrd = refract(rd, normal, 0.75);
         vec3 nro = hit;
         dm = MarchFloor(nro, nrd, hit, ballPos);
         res.rgb = mix(res.rgb, ShadingFloor(hit, nrd, GetNormalFloor(hit, ballPos), nro, dm.y, ballPos), pow(dm.x,0.2));

         // we keep track of fresnel value from ShadingSurface() in the alpha value
         // use this for the reflection frensel calculation
         if (res.a > 0.0)
         {
            vec3 refrd = reflect(rd, normal);
            // tint reflection back to blue a little - sky above sun should be more blue
            res.rgb = mix(res.rgb, Sky(rd)+vec3(0.0,0.2,0.2), res.a);
         }
      }
   }
   else res.rgb = Sky(rd);
   res.rgb = Fog(res.rgb, rd, fogDistance);

#else
   /*vec2 p;
   float d_ang = 2.*PI / float(ANTIALIAS_SAMPLES);
   float ang = d_ang * 0.33333;
   float r = 0.3;
   for (int i = 0; i < ANTIALIAS_SAMPLES; i++)
   {
      p = vec2((gl_FragCoord.x + cos(ang)*r) / resolution.x, (gl_FragCoord.y + sin(ang)*r) / resolution.y);
      vec3 ro = cameraPos + off;
      vec3 rd = normalize(GetRay((cameraLookat-off)-cameraPos+off, p));
   
      vec4 _res;
      vec4 hit = MarchSurface(ro, rd);
      float fogDistance = length(ro-hit.xyz);
      if (hit.a != 0.0)
      {
         vec3 normal = GetNormalSurface(hit.xyz);
         _res = ShadingWater(hit.xyz, rd, normal, ro);
         float ndotr = dot(rd, normal);
         // refract ray
         vec3 nrd = normalize(rd + (-cos(REFRACTION_INDEX*acos(-ndotr))-ndotr) * normal);
         vec3 nro = hit.xyz * nrd;
         // no march needed to hit a flat floor plane, just a single distance test
         hit = vec4(nro, DistFloor(nro));
         _res.rgb = mix(_res.rgb, ShadingFloor(hit.xyz, nrd, GetNormalFloor(hit.xyz), nro), exp(hit.a));

         // we keep track of fresnel value from ShadingWater() in the alpha value
         // use this for the reflection frensel calculation
         if (_res.a > 0.0)
         {
            vec3 refrd = reflect(rd, normal);
            _res.rgb = mix(_res.rgb, Sky(refrd), _res.a);
         }
      }
      else _res.rgb = Sky(rd);
      _res.rgb = Fog(_res.rgb, rd, fogDistance);

      ang += d_ang;
      res.rgb += _res.rgb;*/
   }
   res /= float(ANTIALIAS_SAMPLES);
#endif

   if (postEffects) res.rgb = PostEffects(res.rgb, p);
   
   gl_FragColor = vec4(res.rgb, 1.0);
}
      </script>

<script type="text/javascript">
   var requestAnimationFrame = window.requestAnimationFrame || window.webkitRequestAnimationFrame ||
      window.mozRequestAnimationFrame || window.msRequestAnimationFrame ||
      function (c) { window.setTimeout(c, 15) };
   var config = {
      camera: {
         x: 0.0, y: 1.3, z: -4.0
      },
      lookat: {
         x: 0.0, y: 1.0, z: 0.0
      },
      sunDir: {
         x: -1.0, y: 1.2, z: 4.0
      },
      sunColour: {
         r: 1.1, g: 0.9, b: 0.7
      },
      surface: {
         specular: 32.0,
         specularHardness: 512.0,
         diffuse: 1.0
         //attenDepth: -0.25,
         //attenScale: 0.2
      },
      global: {
         fog: 0.03,
         reflections: true,
         postEffects: true,
         moveCamera: false,
         floorDepth: 3.0,
         param: 0.5
      }
   };

   var pause = false;
   var stats = new Stats();
   function init() {
      var img = new Image();
      img.onload = function () {
         loaded([img]);
      };
      img.src = "./sand2.png";
   }

   var _images;
   var aspect, gl;
   function loaded(images) {
      _images = images;
      document.addEventListener('keydown', function (e) {
         switch (e.keyCode) {
            case 32:
            case 27: // ESC
               pause = !pause;
               break;
         }
      }, false);

      // add GUI controls
      var mobile = (navigator.userAgent.indexOf("Android") !== -1);


      document.body.appendChild(stats.domElement);

      // create webgl context on the canvas element
      var canvas = document.getElementById("canvas");
      aspect = canvas.width / canvas.height;
      try {
         gl = canvas.getContext("experimental-webgl");
      }
      catch (e) {
         document.write("Whoops! No useful WEB-GL impl available. Shame on you and your browser vendor.<br>" + e.message);
         return;
      }
      gl.viewport(0, 0, canvas.width, canvas.height);
      gl.clearColor(0, 0, 0, 1);
      gl.clear(gl.COLOR_BUFFER_BIT);

      // textures
      var sandTexture = gl.createTexture();
      gl.bindTexture(gl.TEXTURE_2D, sandTexture);
      gl.texImage2D(gl.TEXTURE_2D, 0, gl.RGBA, gl.RGBA, gl.UNSIGNED_BYTE, _images[0]);
      gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MAG_FILTER, gl.LINEAR);
      gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MIN_FILTER, gl.LINEAR_MIPMAP_NEAREST);
      gl.generateMipmap(gl.TEXTURE_2D);
      gl.bindTexture(gl.TEXTURE_2D, null);

      // get the vertex and fragment shader source
      var v = document.getElementById("vertex").firstChild.nodeValue;
      var f = document.getElementById("fragment").firstChild.nodeValue;

      // compile and link the shaders
      var vs = gl.createShader(gl.VERTEX_SHADER);
      gl.shaderSource(vs, v);
      gl.compileShader(vs);

      var fs = gl.createShader(gl.FRAGMENT_SHADER);
      gl.shaderSource(fs, f);
      gl.compileShader(fs);

      var program = gl.createProgram();
      gl.attachShader(program, vs);
      gl.attachShader(program, fs);
      gl.linkProgram(program);

      // debug shader compile status
      var error = false;
      if (!gl.getShaderParameter(vs, gl.COMPILE_STATUS)) {
         error = true;
         console.log(gl.getShaderInfoLog(vs));
      }

      if (!gl.getShaderParameter(fs, gl.COMPILE_STATUS)) {
         error = true;
         console.log(gl.getShaderInfoLog(fs));
      }

      if (!gl.getProgramParameter(program, gl.LINK_STATUS)) {
         error = true;
         console.log(gl.getProgramInfoLog(program));
      }
      if (error) return;

      var firstTime = Date.now();
      (f = function () {
         if (!pause) {
            stats.begin();

            // create vertices to fill the canvas with a single quad 
            var vertices = new Float32Array(
               [
                  -1, 1 * aspect, 1, 1 * aspect, 1, -1 * aspect,
                  -1, 1 * aspect, 1, -1 * aspect, -1, -1 * aspect
               ]);

            var vbuffer = gl.createBuffer();
            gl.bindBuffer(gl.ARRAY_BUFFER, vbuffer);
            gl.bufferData(gl.ARRAY_BUFFER, vertices, gl.STATIC_DRAW);

            var triCount = 2,
               numItems = vertices.length / triCount;

            gl.useProgram(program);

            var time = (Date.now() - firstTime) / 1000.0;
            program.time = gl.getUniformLocation(program, "time");
            gl.uniform1f(program.time, time);

            program.resolution = gl.getUniformLocation(program, "resolution");
            gl.uniform2f(program.resolution, canvas.width, canvas.height);

            program.cameraPos = gl.getUniformLocation(program, "cameraPos");
            gl.uniform3f(program.cameraPos, config.camera.x, config.camera.y, config.camera.z);

            program.cameraLookat = gl.getUniformLocation(program, "cameraLookat");
            gl.uniform3f(program.cameraLookat, config.lookat.x, config.lookat.y, config.lookat.z);

            program.sunDir = gl.getUniformLocation(program, "sunDir");
            // pre normalise light dir
            var x = config.sunDir.x, y = config.sunDir.y, z = config.sunDir.z;
            var len = x * x + y * y + z * z;
            len = 1.0 / Math.sqrt(len);
            gl.uniform3f(program.sunDir, config.sunDir.x * len, config.sunDir.y * len, config.sunDir.z * len);

            program.sunColour = gl.getUniformLocation(program, "sunColour");
            gl.uniform3f(program.sunColour, config.sunColour.r, config.sunColour.g, config.sunColour.b);

            program.specular = gl.getUniformLocation(program, "specular");
            gl.uniform1f(program.specular, config.surface.specular);

            program.specularHardness = gl.getUniformLocation(program, "specularHardness");
            gl.uniform1f(program.specularHardness, config.surface.specularHardness);

            program.diffuse = gl.getUniformLocation(program, "diffuse");
            gl.uniform3f(program.diffuse, config.surface.diffuse, config.surface.diffuse, config.surface.diffuse);

            program.moveCamera = gl.getUniformLocation(program, "moveCamera");
            gl.uniform1f(program.moveCamera, config.global.moveCamera);

            program.postEffects = gl.getUniformLocation(program, "postEffects");
            gl.uniform1f(program.postEffects, config.global.postEffects);

            program.reflections = gl.getUniformLocation(program, "reflections");
            gl.uniform1f(program.reflections, config.global.reflections);

            program.fog = gl.getUniformLocation(program, "fog");
            gl.uniform1f(program.fog, config.global.fog);

            program.attenDepth = gl.getUniformLocation(program, "attenDepth");
            gl.uniform1f(program.attenDepth, config.surface.attenDepth);

            program.attenScale = gl.getUniformLocation(program, "attenScale");
            gl.uniform1f(program.attenScale, config.surface.attenScale);

            program.floorDepth = gl.getUniformLocation(program, "floorDepth");
            gl.uniform1f(program.floorDepth, config.global.floorDepth);

            program.param = gl.getUniformLocation(program, "param");
            gl.uniform1f(program.param, config.global.param);

            gl.bindTexture(gl.TEXTURE_2D, sandTexture);
            gl.uniform1i(gl.getUniformLocation(program, "sandTexture"), 0);

            program.aVertexPosition = gl.getAttribLocation(program, "aVertexPosition");
            gl.enableVertexAttribArray(program.aVertexPosition);
            gl.vertexAttribPointer(program.aVertexPosition, triCount, gl.FLOAT, false, 0, 0);

            gl.drawArrays(gl.TRIANGLES, 0, numItems);

            stats.end();
            //pause = true;
         }
         requestAnimationFrame(f);
      })();
   }
</script>

</html>